
&НаСервере
Процедура ВыгрузитьНаСервере()
	
	ФорматЦены			= "ЧРД=.; ЧГ=";
	ФорматКоличества	= "ЧРД=.; ЧГ=";
	ФорматДаты			= "ДФ=yyyyMMdd";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПоставщикуЗапасы.НомерСтроки КАК НомерСтроки,
	|	ЗаказПоставщикуЗапасы.Номенклатура.Артикул КАК Артикул,
	|	ЗаказПоставщикуЗапасы.Количество КАК Количество,
	|	ЗаказПоставщикуЗапасы.Цена КАК Цена
	|ИЗ
	|	Документ.ЗаказПоставщику.Запасы КАК ЗаказПоставщикуЗапасы
	|ГДЕ
	|	ЗаказПоставщикуЗапасы.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Запрос.УстановитьПараметр("Ссылка", ЗаказПоставщику);
	РезультатЗапроса = Запрос.Выполнить();
	
	Текст = Новый ТекстовыйДокумент;
	
	ИдТранзакции = НачЗначTransID;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Стр = "TRANS_ID="		+ Формат(ИдТранзакции, "ЧГ=") + ";"
			+ "ACCOUNT="		+ Счет + ";"
			+ "CLIENT_CODE="	+ КодКлиента + ";"
			+ "CLASSCODE="		+ КодКласса + ";"
			+ "SECCODE="		+ Выборка.Артикул + ";"
			+ "OPERATION="		+ "B" + ";"	// buy
			+ "QUANTITY="		+ Формат(Выборка.Количество, ФорматКоличества) + ";"
		;
		
		Если ВидЗаявки = "Лимитированная" Тогда
			Стр = Стр	
				+ "ACTION="					+ "NEW_ORDER" + ";"
				+ "TYPE="					+ "L" + ";"	// limit
				+ "PRICE="					+ Формат(Выборка.Цена, ФорматЦены) + ";"
			;	
			
		ИначеЕсли ВидЗаявки = "Тэйк-профит" Тогда
			
			Стр = Стр
				+ "ACTION="					+ "NEW_STOP_ORDER" + ";"
				+ "STOP_ORDER_KIND="		+ "TAKE_PROFIT_STOP_ORDER" + ";"
				+ "STOPPRICE="				+ Формат(Выборка.Цена, ФорматЦены) + ";"
				+ "EXPIRY_DATE="			+ Формат(ЗаказПоставщику.ДатаПоступления, ФорматДаты) + ";"
				+ "OFFSET="					+ Формат(ОтступПроц, ФорматЦены) + ";"
				+ "OFFSET_UNITS="			+ "PERCENTS" + ";"
				+ "SPREAD="					+ Формат(ЗащитныйСпрэдПроц, ФорматЦены) + ";"
				+ "SPREAD_UNITS="			+ "PERCENTS" + ";"
			;	
			
		ИначеЕсли ВидЗаявки = "Стоп-цена по другой бумаге" Тогда
			
			Стр = Стр
				+ "ACTION="					+ "NEW_STOP_ORDER" + ";"
				+ "STOP_ORDER_KIND="		+ "CONDITION_PRICE_BY_OTHER_SEC" + ";"
				+ "STOPPRICE_CLASSCODE="	+ КодКласса + ";"
				+ "STOPPRICE_SECCODE="		+ Выборка.Артикул + ";"
				+ "STOPPRICE_CONDITION="	+ "<=" + ";"
				+ "STOPPRICE="				+ Формат(Выборка.Цена, ФорматЦены) + ";"
				+ "PRICE="					+ Формат(Выборка.Цена, ФорматЦены) + ";"
				+ "EXPIRY_DATE="			+ Формат(ЗаказПоставщику.ДатаПоступления, ФорматДаты) + ";"
			;	
			
		КонецЕсли; 
		
		Текст.ДобавитьСтроку(Стр);
		
		ИдТранзакции = ИдТранзакции + 1;
		
	КонецЦикла; 
	
	Текст.Записать(ИмяФайла);
	
	НачЗначTransID = ИдТранзакции;
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ВыгрузитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачЗначTransID = День(ТекущаяДатаСеанса()) * 1000 + 1;
	
	СЗ = Элементы.КодКласса.СписокВыбора;
	СЗ.Добавить("TQBR");
	СЗ.Добавить("TQOB");
	СЗ.Добавить("EQOB");
	
КонецПроцедуры
