
&НаСервере
Процедура ВыгрузитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПоставщикуЗапасы.НомерСтроки КАК НомерСтроки,
	|	ЗаказПоставщикуЗапасы.Номенклатура.Артикул КАК Артикул,
	|	ЗаказПоставщикуЗапасы.Количество КАК Количество,
	|	ЗаказПоставщикуЗапасы.Цена КАК Цена
	|ИЗ
	|	Документ.ЗаказПоставщику.Запасы КАК ЗаказПоставщикуЗапасы
	|ГДЕ
	|	ЗаказПоставщикуЗапасы.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Запрос.УстановитьПараметр("Ссылка", ЗаказПоставщику);
	РезультатЗапроса = Запрос.Выполнить();
	
	Текст = Новый ТекстовыйДокумент;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИдТранзакции = НачЗначTransID - 1 + Выборка.НомерСтроки;
		
		Стр = "TRANS_ID="		+ Формат(ИдТранзакции, "ЧГ=") + ";"
			+ "ACCOUNT="		+ Счет + ";"
			+ "CLIENT_CODE="	+ КодКлиента + ";"
			+ "CLASSCODE="		+ "TQBR" + ";"
			+ "SECCODE="		+ Выборка.Артикул + ";"
		;
		
		Если ВидЗаявки = "Лимитированная" Тогда
			Стр = Стр	
				+ "ACTION="			+ "NEW_ORDER" + ";"
				+ "OPERATION="		+ "B" + ";"	// buy
				+ "TYPE="			+ "L" + ";"	// limit
				+ "PRICE="			+ Формат(Выборка.Цена, "ЧРД=.; ЧГ=") + ";"
				+ "QUANTITY="		+ Формат(Выборка.Количество, "ЧРД=.; ЧГ=") + ";"
			;	
			
		ИначеЕсли ВидЗаявки = "Тэйк-профит" Тогда
			
			Стр = Стр
				+ "ACTION="				+ "NEW_STOP_ORDER" + ";"
				+ "STOP_ORDER_KIND="	+ "TAKE_PROFIT_STOP_ORDER" + ";"
				+ "OPERATION="			+ "B" + ";"	// buy
				+ "STOPPRICE="			+ Формат(Выборка.Цена, "ЧРД=.; ЧГ=") + ";"
				+ "QUANTITY="			+ Формат(Выборка.Количество, "ЧРД=.; ЧГ=") + ";"
				+ "EXPIRY_DATE="		+ Формат(ЗаказПоставщику.ДатаПоступления, "ДФ=yyyyMMdd") + ";"
				+ "OFFSET="				+ "0.05" + ";"
				+ "OFFSET_UNITS="		+ "PERCENTS" + ";"
				+ "SPREAD="				+ "0.05" + ";"
				+ "SPREAD_UNITS="		+ "PERCENTS" + ";"
			;	
		
		КонецЕсли; 
		
		Текст.ДобавитьСтроку(Стр);
		
	КонецЦикла; 
	
	Текст.Записать(ИмяФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ВыгрузитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачЗначTransID = 1;
	
КонецПроцедуры
