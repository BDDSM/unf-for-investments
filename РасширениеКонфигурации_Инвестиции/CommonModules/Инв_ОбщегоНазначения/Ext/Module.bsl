

Процедура УстановитьВремяДокумента(ДокументОбъект, Знач ПараметрОснование = Неопределено) Экспорт
	
	ЧасыВремениДокументов = Новый Соответствие;
	ЧасыВремениДокументов.Вставить(Тип("ДокументСсылка.ПриходнаяНакладная")	, 9); // 9:00
	ЧасыВремениДокументов.Вставить(Тип("ДокументСсылка.РасходнаяНакладная")	, 12);
	ЧасыВремениДокументов.Вставить(Тип("ДокументСсылка.ПоступлениеВКассу")	, 15);
	ЧасыВремениДокументов.Вставить(Тип("ДокументСсылка.ПоступлениеНаСчет")	, 15);
	ЧасыВремениДокументов.Вставить(Тип("ДокументСсылка.РасходИзКассы")		, 18);
	ЧасыВремениДокументов.Вставить(Тип("ДокументСсылка.РасходСоСчета")		, 18);

	ЧасДокумента = ЧасыВремениДокументов.Получить(ТипЗнч(ДокументОбъект.Ссылка));
	Если ЧасДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		НоваяДата = ДокументОбъект.Дата;
		Если НЕ ЗначениеЗаполнено(НоваяДата) Тогда
			НоваяДата = ТекущаяДатаСеанса();
		КонецЕсли;
		//Если НЕ ЗначениеЗаполнено(ПараметрОснование) Тогда
		//	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ДокументОснование", ДокументОбъект.Метаданные())
		//		И ЗначениеЗаполнено(ДокументОбъект.ДокументОснование) Тогда
		//		ПараметрОснование = ДокументОбъект.ДокументОснование;
		//	КонецЕсли;
		//КонецЕсли;
		Если ЗначениеЗаполнено(ПараметрОснование) Тогда
			Если ТипЗнч(ПараметрОснование) = Тип("Структура") Тогда
				Для Каждого КлючИЗначение Из ПараметрОснование Цикл
					Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(КлючИЗначение.Значение)) Тогда
						НоваяДата = КлючИЗначение.Значение.Дата;
						Прервать;
					КонецЕсли; 					
				КонецЦикла; 
			ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ПараметрОснование)) Тогда
				НоваяДата = ПараметрОснование.Дата;
			КонецЕсли; 
		КонецЕсли;
		ДокументОбъект.Дата = НоваяДата;	
	КонецЕсли;
	
	ДокументОбъект.Дата = НачалоДня(ДокументОбъект.Дата) + ЧасДокумента * 60 * 60;
	
КонецПроцедуры

Процедура ПроверитьЗаполнитьНазначениеПлатежа(ДокументОбъект) Экспорт
	
	ЭтоПрочийРасход	= (ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РасходСоСчета") И
						ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.НаРасходы);
	ЭтоПрочийДоход	= (ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеНаСчет") И
						ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.Прочее);
						
	Если ЭтоПрочийРасход Или ЭтоПрочийДоход Тогда		
		Если НЕ ЗначениеЗаполнено(ДокументОбъект.НазначениеПлатежа) Тогда
			ДокументОбъект.НазначениеПлатежа = Строка(ДокументОбъект.Корреспонденция) + ": " + Строка(ДокументОбъект.АналитикаПрочихДоходовИРасходов);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры